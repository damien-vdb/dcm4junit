gradleProjects {
    subprojects {
        path('*') {

            dependencies {

                compileOnly platform(libs.junit.bom)
                testImplementation platform(libs.junit.bom)
                testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

                // DCM4CHE
                implementation libs.dcm4che.core
                compileOnly libs.dcm4che.net

                // Testing
                compileOnly libs.junit.jupiter.api
                testImplementation libs.junit.jupiter
                testImplementation libs.assertj.core
            }

            tasks.named('test', Test) {
                useJUnitPlatform()
            }

            compileJava {
                options.release = 17
            }
        }

        path(':dcm4junit-test') {
            dependencies {
                testImplementation libs.dcm4che.net
                testImplementation(libs.dcm4che.tool.storescu) {
                    exclude group: 'ch.qos.logback', module: 'logback-classic'
                }
            }

            config {
                publishing { enabled = true }
                info {
                    description = 'DICOM test utilities and assertions for Java'
                }
            }
        }

        path(':dcm4junit-spring-boot-test') {

            dependencies {
                implementation project(':dcm4junit-test')

                testImplementation libs.dcm4che.net

                implementation platform(libs.spring.boot.bom)
                implementation libs.spring.context
                implementation libs.spring.test
                testImplementation libs.spring.boot.starter.test
            }

            config {
                publishing { enabled = true }
                info {
                    description = 'Autowire DIMSE Services with Spring Boot in JUnit 5 tests'
                }
            }
        }
    }
}

apply from: "${rootProject.projectDir}/gradle/config.gradle"
apply from: "${rootProject.projectDir}/gradle/jreleaser.gradle"
